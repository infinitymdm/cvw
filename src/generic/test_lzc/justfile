_default:
    @just --list

clean:
    rm -rf verilator_build
    rm -rf logs
    rm -f vivad*.{jou,log}
    rm -rf db incremental_db
    rm -f *.{qpf,qsf}
    rm -rf quartus/outputs_*
    rm -rf alib-52
    rm -rf cksum_dir
    rm -f *.{mr,svf,pvl,syn}
    rm -f genus.*

verilator-test VLEN_MAX:
    @for i in {1..{{VLEN_MAX}}}; do \
        just verilator-run $i; \
    done

verilator-run VLEN:
    verilator --quiet --binary --timing --trace-fst -Wno-lint \
        -MAKEFLAGS "--silent" \
        --Mdir verilator_build \
        -j `nproc` \
        --top tb \
        -DVLEN={{VLEN}} \
        ../lzc.sv lzc_unopt.sv tb.sv lzc_prior.sv
    cd verilator_build && ./Vtb +verilator+quiet && cd ..

_synth-prep:
    @mkdir -p logs/{recursive_opt,recursive_unopt,prior}

yosys-synth: _synth-prep
    yosys -p 'prep -top lzc' ../lzc.sv 2>&1 | tee logs/recursive_opt/yosys.log
    yosys -p 'prep -top lzc_unopt' lzc_unopt.sv 2>&1 | tee logs/recursive_unopt/yosys.log
    yosys -p 'prep -top lzc_prior' lzc_prior.sv 2>&1 | tee logs/prior/yosys.log # error expected

quartus-synth: _synth-prep
    quartus_sta -t quartus/quartus_recursive_opt.tcl 2>&1 | tee logs/recursive_opt/quartus.log
    quartus_sta -t quartus/quartus_recursive_unopt.tcl 2>&1 | tee logs/recursive_unopt/quartus.log
    quartus_sta -t quartus/quartus_prior.tcl 2>&1 | tee logs/prior/quartus.log # error expected

dc-synth: _synth-prep
    dc_shell-xg-t -f design_compiler/dc_recursive.tcl | tee logs/dc_recursive.log
    dc_shell-xg-t -f design_compiler/dc_prior.tcl | tee logs/dc_prior.log

genus-synth: _synth-prep
    genus -f genus/genus_recursive.tcl | tee logs/genus_recursive.log
    genus -f genus/genus_prior.tcl | tee logs/genus_prior.log

vivado-synth: _synth-prep
    vivado -mode batch -source vivado/vivado_recursive.tcl | tee logs/vivado_recursive.log
    vivado -mode batch -source vivado/vivado_prior.tcl | tee logs/vivado_prior.log

synth-all:
    @just clean
    @just yosys-synth
    @just quartus-synth
    @just vivado-synth
    @just dc-synth
    @just genus-synth
